---
title: "Upgrading to the new ODE interface"
date: "12 July 2020"
output: html_document
---

Cmdstan 2.24 introduces a new ODE interface intended to make it easier to use
complicated data structures when working with ODEs. As such, there are a few
significant user-facing changes that someone solving ODEs in Stan should be
aware of:

* In the old ODE interface, parameters and data that are going to be used in
evaluating the ODE system function are packed into one of three arrays, a
`real[]` array for parameters, a `real[]` array for data that are reals,
and an `int[]` array for integer data. These three arrays are passed by Stan
from the ODE solve function call to the ODE system function.

  In the new interface, no packing is necesary. Any argument (parameter
or data) can be passed from the ODE solve function to the ODE system function
by appending it to the end of the ODE solve function call (and adding a
matching argument declaration in the ODE system function).

* The new interface also uses `vector` variables instead of `real[]` variables
for the states.

* The new interface functions are renamed `ode_rk45`, `ode_bdf`, `ode_adams`.
These have default tolerances. The separate functions `ode_rk45_tol`,
`ode_bdf_tol`, and `ode_rk45_adams` can be used to solve ODEs with specified
tolerances.

This guide is meant to show examples of using the new ODE interface as compared
to the old one. For detailed information about either interface, look at the
function reference guide:
[Old interface](https://mc-stan.org/docs/2_23/functions-reference/functions-ode-solver.html),
[New interface](https://mc-stan.org/docs/2_23/functions-reference/functions-ode-solver.html)

The two models here come from the Stan
[Statistical Computation Benchmarks](https://github.com/stan-dev/stat_comp_benchmarks).

# SIR Model

## ODE System Function

In the old SIR system function, `beta`, `kappa`, `gamma`, `xi`, and `delta`,
are packed into the `real[] theta` argument. `kappa` isn't actually a model
parameter so it is not clear why it is packed in with the other parameters,
but it is. Promoting `kappa` to a parameter causes there to be more states
in the extended ODE sensitivity system (used to get gradients of the ODE
with respect to inputs). The ODE system function looks like:

```{stan, output.var = "", eval = FALSE}
functions {
  // theta[1] = beta, water contact rate
  // theta[2] = kappa, C_{50}
  // theta[3] = gamma, recovery rate
  // theta[4] = xi, bacteria production rate
  // theta[5] = delta, bacteria removal rate
  real[] simple_SIR(real t,
                    real[] y,
                    real[] theta,
                    real[] x_r,
                    int[] x_i) {
    real dydt[4];

    dydt[1] = - theta[1] * y[4] / (y[4] + theta[2]) * y[1];
    dydt[2] = theta[1] * y[4] / (y[4] + theta[2]) * y[1] - theta[3] * y[2];
    dydt[3] = theta[3] * y[2];
    dydt[4] = theta[4] * y[2] - theta[5] * y[4];

    return dydt;
  }
}
```

For comparison, with the new interface the ODE system function can be
rewritten to explicitly name all the parameters. No separation of data
and parameters is necessary either -- the solver will not add more
equations for arguments that are defined in the `data` and
`transformed data` blocks. The state variables in the new model are also
represented by `vector` variables instead of `real[]` variables.
The new ODE system function is:

```{stan, output.var = "", eval = FALSE}
functions {
  vector simple_SIR(real t,
                    vector y,
                    real beta,    // water contact rate
                    real kappa,   // C_{50}
                    real gamma,   // recovery rate
                    real xi,      // bacteria production rate
                    real delta) { // bacteria removal rate
    vector[4] dydt;

    dydt[1] = -beta * y[4] / (y[4] + kappa) * y[1];
    dydt[2] = beta * y[4] / (y[4] + kappa) * y[1] - gamma * y[2];
    dydt[3] = gamma * y[2];
    dydt[4] = xi * y[2] - delta * y[4];

    return dydt;
  }
}
```

## Calling the ODE Solver

In the old ODE interface, the parameters are all packed into a `real[]` array
before calling the ODE solver:

```{stan, output.var = "", eval = FALSE}
transformed parameters {
  real<lower=0> y[N_t, 4];
  {
    real theta[5] = {beta, kappa, gamma, xi, delta};
    y = integrate_ode_rk45(simple_SIR, y0, t0, t, theta, x_r, x_i);
  }
}
```

In the new ODE interface each of the arguments is appended on to the ODE
solver function call. The RK45 ODE solver with default tolerances is called
`ode_rk45`. Because the states are handled as `vector` variables, the solver
output is an array of vectors (`vector[]`).

```{stan, output.var = "", eval = FALSE}
transformed parameters {
  vector<lower=0>[4] y[N_t];
  {
    y = ode_rk45(simple_SIR, y0, t0, t, beta, kappa, gamma, xi, delta);
  }
}
```

## Full Model

The full model with the new interface can be found
[here](https://github.com/stan-dev/stat_comp_benchmarks/blob/master/benchmarks/sir/sir.stan);
the full model with the old interface can be found
[here](https://github.com/stan-dev/stat_comp_benchmarks/blob/master/benchmarks/sir/sir.stan),
and the data can be found
[here](https://github.com/stan-dev/stat_comp_benchmarks/blob/master/benchmarks/sir/sir.data.R).

# PKPD Model

## ODE System Function

In the old system function, parameters are manually unpacked from `theta` and
`x_r`:

```
functions {
  real[] one_comp_mm_elim_abs(real t,
                              real[] y,
                              real[] theta,
                              real[] x_r,
                              int[] x_i) {
    real dydt[1];
    real k_a = theta[1]; // Dosing rate in 1/day
    real K_m = theta[2]; // Michaelis-Menten constant in mg/L
    real V_m = theta[3]; // Maximum elimination rate in 1/day
    real D = x_r[1];
    real V = x_r[2];
    real dose = 0;
    real elim = (V_m / V) * y[1] / (K_m + y[1]);

    if (t > 0)
      dose = exp(- k_a * t) * D * k_a / V;

    dydt[1] = dose - elim;

    return dydt;
  }
}
```

In the new interface, they are passed directly and so the unpacking is avoided:

```
functions {
  vector one_comp_mm_elim_abs(real t,
                              vector y,
                              real k_a, // Dosing rate in 1/day
                              real K_m, // Michaelis-Menten constant in mg/L
                              real V_m, // Maximum elimination rate in 1/day
                              real D,
                              real V) {
    vector[1] dydt;

    real dose = 0;
    real elim = (V_m / V) * y[1] / (K_m + y[1]);

    if (t > 0)
      dose = exp(- k_a * t) * D * k_a / V;

    dydt[1] = dose - elim;

    return dydt;
  }
}
```

## Calling the ODE Solver

In the old interface the `theta` and `x_r` arguments are packed manually, and
the `x_i` argument is required even though it isn't used:

```
transformed data {
  real x_r[2] = {D, V};
  int x_i[0];
}
...
transformed parameters {
  real C[N_t, 1];
  {
    real theta[3] = {k_a, K_m, V_m};
    C = integrate_ode_bdf(one_comp_mm_elim_abs, C0, t0, times, theta, x_r, x_i);
  }
}
```

In the new interface the arguments are simply passed at the end of the
`ode_bdf` call:

```
transformed parameters {
  vector[1] C[N_t];
  {
    real theta[3] = {k_a, K_m, V_m};
    C = ode_bdf(one_comp_mm_elim_abs, C0, t0, times, k_a, K_m, V_m, D, V);
  }
}
```
## Full Model

The full model with the new interface can be found
[here](https://github.com/stan-dev/stat_comp_benchmarks/blob/master/benchmarks/pkpd/one_comp_mm_elim_abs.stan);
the full model with the old interface can be found
[here](https://github.com/stan-dev/stat_comp_benchmarks/blob/master/benchmarks/pkpd/one_comp_mm_elim_abs.stan),
and the data can be found
[here](https://github.com/stan-dev/stat_comp_benchmarks/blob/master/benchmarks/pkpd/one_comp_mm_elim_abs.data.R).

## Generated Quantities Only Model

There is an additional PKPD model that only uses generated quantities. It has
been converted to the new interface as well.

The full model with the new interface can be found
[here](https://github.com/stan-dev/stat_comp_benchmarks/blob/master/benchmarks/pkpd/sim_one_comp_mm_elim_abs.stan);
the full model with the old interface can be found
[here](https://github.com/stan-dev/stat_comp_benchmarks/blob/master/benchmarks/pkpd/sim_one_comp_mm_elim_abs.stan),
and the data can be found
[here](https://github.com/stan-dev/stat_comp_benchmarks/blob/master/benchmarks/pkpd/one_comp_mm_elim_abs.data.R).

